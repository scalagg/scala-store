plugins {
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "org.jetbrains.kotlin.jvm" version "1.6.0"
}

allprojects {
    group "gg.scala.store"
    version "0.1.0"

    repositories {
        mavenCentral()
        mavenLocal()

        maven { url "https://jitpack.io" }
    }
}

subprojects {
    apply plugin: "maven-publish"
    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: "org.jetbrains.kotlin.jvm"

    dependencies {
        compileOnly "org.jetbrains.kotlin:kotlin-stdlib"

        compileOnly "redis.clients:jedis:4.0.1"
        compileOnly "org.mongodb:mongo-java-driver:3.12.10"
        compileOnly "com.google.code.gson:gson:2.8.9"
    }

    compileKotlin {
        kotlinOptions.javaParameters = true
        kotlinOptions.jvmTarget = "1.8"
    }

    shadowJar {
        archiveClassifier.set("")

        exclude "**/*.kotlin_metadata"
        exclude "**/*.kotlin_builtins"
        exclude "META-INF/"

        archiveFileName = "store-${project.name}.jar"
    }

    publishing {
        publications {
            shadow(MavenPublication) { publication ->
                project.shadow.component(publication)
            }
        }
    }

    tasks.build.dependsOn(shadowJar, publishShadowPublicationToMavenLocal)
}
