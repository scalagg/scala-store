import org.gradle.api.publish.maven.MavenPublication

plugins {
    id "maven-publish"
    id "com.jfrog.artifactory" version "4.28.2"
    id "org.jetbrains.kotlin.jvm" version "1.6.20"
    id "org.jetbrains.kotlin.kapt" version "1.6.20"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

allprojects {
    group "gg.scala.store"
    version "0.1.3"

    repositories {
        mavenCentral()

        maven { url "https://repo.stellardrift.ca" }
        maven { url "https://jitpack.io" }

        maven {
            name "scala"
            url "${artifactory_contextUrl}/${artifactory_release_local}"
            credentials {
                username "${artifactory_user}"
                password "${artifactory_password}"
            }
        }
    }
}

subprojects {
    apply plugin: "maven-publish"
    apply plugin: "com.jfrog.artifactory"
    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "org.jetbrains.kotlin.kapt"

    dependencies {
        compileOnly "org.jetbrains.kotlin:kotlin-stdlib"

        compileOnly "org.mongodb:mongo-java-driver:3.12.11"
        compileOnly "com.google.code.gson:gson:2.9.0"

        compileOnly "gg.scala.aware:aware:1.0.8.2"
        compileOnly 'io.lettuce:lettuce-core:6.3.0.STABLE'
    }

    compileKotlin {
        kotlinOptions.javaParameters = true
        kotlinOptions.jvmTarget = "1.8"
    }

    shadowJar {
        archiveClassifier.set("")

        exclude "**/*.kotlin_metadata"
        exclude "**/*.kotlin_builtins"
        exclude "META-INF/"

        archiveFileName = "store-${project.name}.jar"
    }

    publishing {
        publications {
            shadow(MavenPublication) { publication ->
                project.shadow.component(publication)
            }
        }

        repositories {
            maven {
                name "scala"
                url "${artifactory_contextUrl}/${artifactory_release}"
                credentials {
                    username "${artifactory_user}"
                    password "${artifactory_password}"
                }
            }
        }
    }

    tasks.build.dependsOn(
        shadowJar,
        publishShadowPublicationToScalaRepository
    )
}

artifactory {
    this.contextUrl = "${artifactory_contextUrl}"

    publish {
        repository {
            repoKey = "${artifactory_release_local}"
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
    }
}
